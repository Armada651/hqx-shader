/* COMPATIBILITY 
   - HLSL compilers
   - Cg   compilers
*/


/*
* Copyright (C) 2003 Maxim Stepin ( maxst@hiend3d.com )
*
* Copyright (C) 2010 Cameron Zemek ( grom@zeminvaders.net )
*
* Copyright (C) 2014 Jules Blok ( jules@aerix.nl )
*
* This program is free software; you can redistribute it and/or
* modify it under the terms of the GNU Lesser General Public
* License as published by the Free Software Foundation; either
* version 2.1 of the License, or (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
* Lesser General Public License for more details.
*
* You should have received a copy of the GNU Lesser General Public
* License along with this program; if not, write to the Free Software
* Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
*/

#define SCALE 3

struct orig
{
	float2 tex_coord;
	uniform float2 texture_size;
	uniform sampler2D texture;
};

struct input
{
	half2 video_size;
	float2 texture_size;
	half2 output_size;
};

struct out_vertex {
	float4 position : POSITION;
	float4 color    : COLOR;
	float2 texCoord : TEXCOORD0;
	float4 t1;
	float4 t2;
	float4 t3;
};

/*    VERTEX_SHADER    */
out_vertex main_vertex
(
	float4 position	 : POSITION,
	float4 color	 : COLOR,
	float2 texCoord1 : TEXCOORD0,

   	uniform half4x4 modelViewProj,
	uniform input IN
)
{
	out_vertex OUT;

	OUT.position = mul(modelViewProj, position);
	OUT.color = color;

	float2 ps = float2(1.0/IN.texture_size.x, 1.0/IN.texture_size.y);
	float dx = ps.x;
	float dy = ps.y;

	//   +----+----+----+
	//   |    |    |    |
	//   | w1 | w2 | w3 |
	//   +----+----+----+
	//   |    |    |    |
	//   | w4 | w5 | w6 |
	//   +----+----+----+
	//   |    |    |    |
	//   | w7 | w8 | w9 |
	//   +----+----+----+

	// This line fix a bug in ATI cards.
	float2 texCoord = texCoord1 + float2(0.0000001, 0.0000001);

	OUT.texCoord = texCoord;
	OUT.t1 = texCoord.xxxy + half4(-dx, 0, dx, -dy); //  w1 | w2 | w3
	OUT.t2 = texCoord.xxxy + half4(-dx, 0, dx,   0); //  w4 | w5 | w6
	OUT.t3 = texCoord.xxxy + half4(-dx, 0, dx,  dy); //  w7 | w8 | w9

	return OUT;
}


/*    FRAGMENT SHADER    */
float4 main_fragment(in out_vertex VAR, uniform sampler2D decal : TEXUNIT0, orig ORIG, uniform input IN, uniform sampler2D LUT : TEXUNIT1) : COLOR
{
	float2 fp = frac(VAR.texCoord*IN.texture_size);
	half2 quad = floor(fp * SCALE);

	half3 w1  = tex2D(ORIG.texture, VAR.t1.xw).rgb;
	half3 w2  = tex2D(ORIG.texture, VAR.t1.yw).rgb;
	half3 w3  = tex2D(ORIG.texture, VAR.t1.zw).rgb;

	half3 w4  = tex2D(ORIG.texture, VAR.t2.xw).rgb;
	half3 w5  = tex2D(ORIG.texture, VAR.t2.yw).rgb;
	half3 w6  = tex2D(ORIG.texture, VAR.t2.zw).rgb;

	half3 w7  = tex2D(ORIG.texture, VAR.t3.xw).rgb;
	half3 w8  = tex2D(ORIG.texture, VAR.t3.yw).rgb;
	half3 w9  = tex2D(ORIG.texture, VAR.t3.zw).rgb;

	half2 index = tex2D(decal, VAR.texCoord).xy * half2(255.0, 15.0 * (SCALE * SCALE));

	half4x3 pixels;
	if (quad.x <= 1 && quad.y <= 1) {
		pixels = half4x3(w5, w1, w2, w4); // PIXEL00, PIXEL01, PIXEL10, PIXEL11
	} else if (quad.x == 2 && quad.y <= 1) {
		pixels = half4x3(w5, w2, w3, w6); // PIXEL02, PIXEL12
	} else if (quad.x <= 1 && quad.y == 2) {
		pixels = half4x3(w5, w4, w7, w8); // PIXEL20, PIXEL21
	} else {
		pixels = half4x3(w5, w6, w8, w9); // PIXEL22
	}

	index.y += dot(quad, half2(1, SCALE));

	half2 step = 1.0 / half2(256.0, 16.0 * (SCALE * SCALE));
	half2 offset = step / 2.0;
	half4 vector = tex2D(LUT, index * step + offset) * 255.0;
	float3 res = mul(transpose(pixels), vector) / dot(vector, half4(1));

	return float4(res, 1.0);
}
